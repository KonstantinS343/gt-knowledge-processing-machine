scp_program -> proc_find_the_connected_components
(*
/////////////////////Генерация связок
	-> rrel_params: ...
	(*
		->rrel_1: rrel_in: _curr_graph;;
		->rrel_2: rrel_out: _components_true;;
	*);;

	-> rrel_operators: ... 
	(*
		->rrel_init:..gen_components_true
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _components_true;;
			
			=> nrel_goto: ..gen_attribut_component;;
		*);;

		->..gen_attribut_component
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _attribut_component;;
			
			=> nrel_goto: ..gen_atrribut_vertex;;
		*);;
		
		->..gen_atrribut_vertex
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _attribut_vertex;;
			
			=> nrel_goto: ..gen_atrribut_for_fifo;;
		*);;
		
		->..gen_atrribut_for_fifo
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _attribut_for_fifo;;
			
			=> nrel_goto: ..set_vertexs;;
		*);;
		
		->..set_vertexs
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _vert;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
                        -> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _vertexs;;
			
			=> nrel_then: ..run_sourse_components;;
                	=> nrel_else: ..erase_attribut_component;;
		*);;

		->..run_sourse_components
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _vertexs;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _curr_vert;;
			
			=>nrel_then: ..gen_components_curr_vert;;
			=>nrel_else: ..erase_vertexs;; 		
		*);;

		->..gen_components_curr_vert
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _components_curr_vert;;
			
			=> nrel_goto: ..gen_lig_components_true_components_curr_vert_attribut_component;;
		*);;
	
		->..gen_lig_components_true_components_curr_vert_attribut_component
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _components_true;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _components_curr_vert;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_component;;
			
			=> nrel_goto: ..gen_lig_components_curr_vert;;
		*);;

		->..gen_lig_components_curr_vert
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _components_curr_vert;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _curr_vert;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_vertex;;
			
			=> nrel_goto: ..erase_arc_erase;;
		*);;
		
		->..erase_arc_erase
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: ..run_sourse_components;;
		*);;
	
		->..erase_vertexs
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _vertexs;;
			
			=> nrel_goto: ..create_level1;;
		*);;

/////////////////////Деление на компоненты сильной связности
/////////////////идем к компоненту,уровень1
		->..create_level1
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _components_true;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _attribut_component;;
			
			-> rrel_set_3: rrel_assign: rrel_scp_var: _level1;;

			=> nrel_then: ..go_level1;;
                	=> nrel_else: ..clean_components_true;;
		*);;

		->..go_level1
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _level1;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _curr_component;;

			=>nrel_then: ..create_level2;;
			=>nrel_else: ..erase_level1;; 		
		*);;

/////////////////идем к вершина текущего компонента, уровень2	

		->..create_level2
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_component;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc11;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc22;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _attribut_vertex;;
			
			-> rrel_set_3: rrel_assign: rrel_scp_var: _level2;;
			
			=> nrel_then: ..go_level2;;
                	=> nrel_else: ..erase_arc_erase1;;
		*);;
 
		->..go_level2
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _level2;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase2;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _curr_vertex_of_curr_component;;
			
			=>nrel_then: ..create_level3;;
			=>nrel_else: ..erase_level2;; 		
		*);;

////////////////идем к соседям текущей вершины, уровень 3
		
		->..create_level3
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_vertex_of_curr_component;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc111;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno1;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc222;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _curr_graph;;
			
			-> rrel_set_3: rrel_assign: rrel_scp_var: _level3;;
			
			=> nrel_then: ..go_level3;;
                	=> nrel_else: ..erase_arc_erase2;;
		*);;

		->..go_level3
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _level3;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase3;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _sosed;;
			
			=> nrel_then: ..check;;
			=> nrel_else: ..erase_level3;; 		
		*);;

		->..check
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_component;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_nevagno;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _sosed;;
			=> nrel_then: ..erase_arc_erase3;;
			=> nrel_else: ..create_fifo;;
		*);;
///////////////////////запускаем очередь от соседней вершины,fifo

		-> ..create_fifo
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _sosed;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc111;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc222;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _curr_graph;;
			
			-> rrel_set_3: rrel_assign: rrel_scp_var: _fifo;;
			
			=> nrel_then: ..call_attribute_set_to_attach;;
                	=> nrel_else: ..erase_arc_erase3;;
		*);;
		
		-> ..call_attribute_set_to_attach
		(*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: attribute_set_to_attach;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... 
			(*	
				-> rrel_1: rrel_fixed: rrel_scp_var: _fifo;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _attribut_for_fifo;;			
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;
		
			=> nrel_goto: ..wait117;;
		*);;

		-> ..wait117
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

		  	=> nrel_goto: ..go_fifo;;
		*);; 

		-> ..go_fifo
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _fifo;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_prosto4;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _qb;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_for_fifo;;
			
			=> nrel_then: ..check_qb_component;;
			=> nrel_else: ..erase_fifo;;
		*);;
	
		->..check_qb_component
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_component;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_nevagno;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _qb;;

			=> nrel_then: ..change_sosed_component;;
			=> nrel_else: ..add_fifo;;
		*);;

////////////////////////Нашли,что искали,завершаем очередь
		->..change_sosed_component
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _sosed_component;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm:  _arc_nev1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _sosed;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_nev2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_vertex;;

			=> nrel_then: ..erase_arc_sosed_component;;
			=> nrel_else: ..gen_new_component;;//..erase_arc_sosed_component;; 
		*);;
		
		->..erase_arc_sosed_component
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_var: rrel_erase: _arc_nev1;;

			=> nrel_goto: ..gen_new_component;;
		*);;

		->..gen_new_component
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_component;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _sosed;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_vertex;;

			=> nrel_goto: ..add_sosed_in_level2;;
		*);;

		->..add_sosed_in_level2
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _level2;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _sosed;;

			=> nrel_goto: ..erase_fifo;;
		*);;
/////////////////////добавляем новый элемент в очередь
		->..add_fifo
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _qb;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc111;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc222;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _curr_graph;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _add_fifo;;

			=> nrel_then: ..go_add_fifo;;
                	=> nrel_else: ..erase_arc_erase4;;
		*);;
		
		-> ..go_add_fifo
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _add_fifo;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase5;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _podoz;;

			=> nrel_then: ..check_podoz;;
			=> nrel_else: ..erase_add_fifo;;
		*);;		

		-> ..check_podoz
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _fifo;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_nevagno;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _podoz;;

			=> nrel_then: ..erase_arc_erase5;;
			=> nrel_else: ..add_podoz_in_fifo;;
  		*);;

		-> ..add_podoz_in_fifo
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _fifo;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc1112;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _podoz;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc1113;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_for_fifo;;

			=> nrel_goto: ..erase_arc_erase5;;
		*);;

//////////////////////////////////////чистим память

		->..erase_arc_erase5
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase5;;

			=> nrel_goto: ..go_add_fifo;;
		*);;

		->..erase_add_fifo
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _add_fifo;;

			=> nrel_goto: ..erase_arc_erase4;;
		*);;

		->..erase_arc_erase4
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase4;;

			=> nrel_goto: ..go_fifo;;
		*);;

		->..erase_fifo
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _fifo;;

			=> nrel_goto: ..erase_arc_erase3;;
		*);;

		->..erase_arc_erase3
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase3;;

			=> nrel_goto: ..go_level3;;
		*);;

		->..erase_level3
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _level3;;

			=> nrel_goto: ..erase_arc_erase2;;
		*);;

		->..erase_arc_erase2
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase2;;

			=> nrel_goto: ..go_level2;;
		*);;

		->..erase_level2
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _level2;;

			=> nrel_goto: ..erase_arc_erase1;;
		*);;
		
		->..erase_arc_erase1
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase1;;

			=> nrel_goto: ..go_level1;;
		*);;

		->..erase_level1
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _level1;;

			=> nrel_goto: ..clean_components_true;;
		*);;

////////////////////////удаляем лишние компоненты у components_true
		
		-> ..clean_components_true
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _components_true;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc111;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc222;;
                        -> rrel_5: rrel_fixed: rrel_scp_var: _attribut_component;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _components;;

			=> nrel_then: ..go_components;;
                	=> nrel_else: ..erase_attribut_component;;
		*);;

		->..go_components
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _components;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_new_erase;;
			-> rrel_3: rrel_assign: rrel_scp_var: _curr_comp;;

			=> nrel_then: ..go_k_veram;;
			=> nrel_else: ..erase_components;;
		*);;

		->..go_k_veram
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_comp;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _nevagno;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _new_arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attribut_vertex;;

			=> nrel_then: ..erase_arc_new_erase;;
			=> nrel_else: ..erase_curr_comp;;
		*);;

		->..erase_curr_comp
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _curr_comp;;

			=> nrel_goto: ..go_components;;
		*);;
	
		->..erase_arc_new_erase
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_new_erase;;

			=> nrel_goto: ..go_components;;
		*);;

		->..erase_components
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _components;;

			=> nrel_goto: ..erase_attribut_component;;
		*);;
		
		->..erase_attribut_component
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _attribut_component;;

			=>nrel_goto: ..erase_attribut_vertex;;
		*);;

		->..erase_attribut_vertex
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _attribut_vertex;;

			=>nrel_goto: ..erase_attribut_for_fifo;;
		*);;

		->..erase_attribut_for_fifo
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _attribut_for_fifo;;

			=>nrel_goto: ..ret;;
		*);;

		->..ret(*<-return;;*);;
	*);;
*);;
