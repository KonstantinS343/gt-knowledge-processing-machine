scp_program -> proc_min_ostov
(*
	-> rrel_params: ...
	(*
		-> rrel_1: rrel_in: _curr_graph;;
		-> rrel_2: rrel_in: _answer;;
	*);;

	-> rrel_operators: ... 
	(*
		-> rrel_init: .proc_min_ostov_operator1
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _ans_tree;;
                        -> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
                        -> rrel_5: rrel_fixed: rrel_scp_const: nrel_min_ostov;;
			
			=> nrel_then: .proc_min_ostov_operator45;;
                	=> nrel_else: .proc_min_ostov_operator3;;
		*);;		
		
		-> .proc_min_ostov_operator3
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _attrib_for_subtrees;;
			=> nrel_goto: .proc_min_ostov_operator4;;
		*);;
		
		-> .proc_min_ostov_operator4
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _vert;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _vertexs;;
			
			=> nrel_then: .proc_min_ostov_operator5;;
                	=> nrel_else: .proc_min_ostov_operator11;;
		*);;

		-> .proc_min_ostov_operator5
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _vertexs;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _curr_vert;;
			
			=>nrel_then: .proc_min_ostov_operator6;;
			=>nrel_else: .proc_min_ostov_operator10;; 	
		*);;

		-> .proc_min_ostov_operator6
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _subtrees;;
			=> nrel_goto: .proc_min_ostov_operator8;;
		*);;

		-> .proc_min_ostov_operator7
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_subtrees;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _subtrees;;

		  	=> nrel_goto: .proc_min_ostov_operator8;;
		*);;

		-> .proc_min_ostov_operator8
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _subtrees;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _curr_vert;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertex2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;

			=> nrel_goto: .proc_min_ostov_operator9;;
		*);;

		-> .proc_min_ostov_operator9
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: .proc_min_ostov_operator5;;
		*);;

		-> .proc_min_ostov_operator10
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _vertexs;;
			
			=> nrel_goto: .proc_min_ostov_operator11;;
		*);;

		-> .proc_min_ostov_operator11
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_link: rrel_const: rrel_scp_var: _min;;

			=> nrel_goto: .proc_min_ostov_operator12;;
		*);;

		-> .proc_min_ostov_operator12
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _ans;;

			=> nrel_goto: .proc_min_ostov_operator13_1;;
		*);;

		-> .proc_min_ostov_operator13_1
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _ans_tree;;

			=> nrel_goto: .proc_min_ostov_operator13;;
		*);;
		
		-> .proc_min_ostov_operator13
		(*
			<- contAssign;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _min;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: [999999];;

			=> nrel_goto: .proc_min_ostov_operator14_1;;
		*);;
		
		-> .proc_min_ostov_operator14_1
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ans;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase_now;;
			-> rrel_3: rrel_assign: rrel_scp_var: _verto2;;	

			=> nrel_then: .proc_min_ostov_operator14_2;;
			=> nrel_else: .proc_min_ostov_operator14;;
		*);;

		-> .proc_min_ostov_operator14_2
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase_now;;
			
			=> nrel_goto: .proc_min_ostov_operator14;;
		*);;

		-> .proc_min_ostov_operator14
		(*
			<- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _ed;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_oredge;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _arcs;;
			
			=> nrel_then: .proc_min_ostov_operator15;;
                	=> nrel_else: .proc_min_ostov_operator27;;
		*);;

		-> .proc_min_ostov_operator15
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _arcs;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _curr_arc;;
			
			=>nrel_then: .proc_min_ostov_operator16;;
			=>nrel_else: .proc_min_ostov_operator26;; 	
		*);;
		
		-> .proc_min_ostov_operator16
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _vertex1;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _curr_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex2;;	

			=> nrel_goto: .proc_min_ostov_operator17;;
		*);;

		-> .proc_min_ostov_operator17
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _curr_set;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _vertex1;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;
			
			=> nrel_then: .proc_min_ostov_operator18;;
                	=> nrel_else: .proc_min_ostov_return;;
		*);;

		-> .proc_min_ostov_operator18
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_set;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _vertex2;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;
			
			=> nrel_then: .proc_min_ostov_operator25;;
                	=> nrel_else: .proc_min_ostov_operator19;;
		*);;
		
		-> .proc_min_ostov_operator19
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_arc;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _ves;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_weight;;
			
			=> nrel_then: .proc_min_ostov_operator20;;
                	=> nrel_else: .proc_min_ostov_return;;
		*);;

		-> .proc_min_ostov_operator20
		(*
			<- ifGr;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _min;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _ves;;

			=> nrel_then: .proc_min_ostov_operator21;;
			=> nrel_else: .proc_min_ostov_operator25;;
		*);;		

		-> .proc_min_ostov_operator21
		(*
			<- contAssign;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _min;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _ves;;

			=> nrel_goto: .proc_min_ostov_operator22;;
		*);;

		-> .proc_min_ostov_operator22
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ans;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase_now;;
			-> rrel_3: rrel_assign: rrel_scp_var: _verto2;;	

			=> nrel_then: .proc_min_ostov_operator23;;
			=> nrel_else: .proc_min_ostov_operator24;;
		*);;

		-> .proc_min_ostov_operator23
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase_now;;
			
			=> nrel_goto: .proc_min_ostov_operator24;;
		*);;

		-> .proc_min_ostov_operator24
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ans;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _curr_arc;;

		  	=> nrel_goto: .proc_min_ostov_operator25;;
		*);;

		-> .proc_min_ostov_operator25
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: .proc_min_ostov_operator15;;
		*);;

		-> .proc_min_ostov_operator26
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arcs;;
			
			=> nrel_goto: .proc_min_ostov_operator27;;
		*);;

		-> .proc_min_ostov_operator27
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ans;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase_now;;
			-> rrel_3: rrel_assign: rrel_scp_var: _add_arc;;	

			=> nrel_then: .proc_min_ostov_operator28;;
			=> nrel_else: .proc_min_ostov_operator41;;
		*);;
		
		-> .proc_min_ostov_operator28
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _ans_tree;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _add_arc;;

		  	=> nrel_goto: .proc_min_ostov_operator29;;
		*);;

		-> .proc_min_ostov_operator29
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _vertex1;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _add_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _vertex2;;	

			=> nrel_goto: .proc_min_ostov_operator30;;
		*);;

		-> .proc_min_ostov_operator30
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _subtree1;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _vertex1;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;
			
			=> nrel_then: .proc_min_ostov_operator31;;
                	=> nrel_else: .proc_min_ostov_return;;
		*);;

		-> .proc_min_ostov_operator31
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _subtree2;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _vertex2;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;
			
			=> nrel_then: .proc_min_ostov_operator32;;
                	=> nrel_else: .proc_min_ostov_return;;
		*);;

		-> .proc_min_ostov_operator32
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _subtree1;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase_n;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _vert;;
			
			=> nrel_then: .proc_min_ostov_operator32_1;;
                	=> nrel_else: .proc_min_ostov_operator33;;
		*);;

		-> .proc_min_ostov_operator32_1
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _subtree2;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _vert;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertex2;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _attrib_for_subtrees;;

			=> nrel_goto: .proc_min_ostov_operator32_2;;
		*);;
	
		-> .proc_min_ostov_operator32_2
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase_n;;
			
			=> nrel_goto: .proc_min_ostov_operator32;;
		*);;

		-> .proc_min_ostov_operator33
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _subtree1;;
			
			=> nrel_goto: .proc_min_ostov_operator13;;
		*);;

		
		-> .proc_min_ostov_operator41
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: rrel_const: _arc_of_vertexs1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _ans_tree;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertex2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_min_ostov;;

			=> nrel_goto: .proc_min_ostov_operator43;;
		*);;			

		-> .proc_min_ostov_operator43
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _min;;
			
			=> nrel_goto: .proc_min_ostov_operator44;;
		*);;
		
		-> .proc_min_ostov_operator44
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _ans;;
			
			=> nrel_goto: .proc_min_ostov_operator45;;
		*);;		
		
		-> .proc_min_ostov_operator45
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _curr_graph;;
			
			=> nrel_goto: .proc_min_ostov_operator46;;
		*);;
	
		-> .proc_min_ostov_operator46
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: rrel_common: _arc_of_vertexs1;;
			
			=> nrel_goto: .proc_min_ostov_operator47;;
		*);;

		-> .proc_min_ostov_operator47
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _ans_tree;;
			
			=> nrel_goto: .proc_min_ostov_operator48;;
		*);;
	
		-> .proc_min_ostov_operator48
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _arc_of_vertex2;;
			
			=> nrel_goto: .proc_min_ostov_operator49;;
		*);;

		-> .proc_min_ostov_operator49
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara1;;
			-> rrel_3: rrel_fixed: rrel_scp_const: nrel_min_ostov;;
			
			=> nrel_goto: .proc_min_ostov_operator50;;
		*);;

		-> .proc_min_ostov_operator50
		(*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const:  proc_add_min_tree;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... 
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _ans_tree;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
			*);;

			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_min_ostov_operator51;;
		*);;
		
		-> .proc_min_ostov_operator51
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_min_ostov_operator52;;
		*);;

		-> .proc_min_ostov_operator52
		(*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_add_graph_in_ans;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... 
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _curr_graph;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
			*);;

			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_min_ostov_operator53;;
		*);;
		
		-> .proc_min_ostov_operator53
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_min_ostov_return;;
		*);;

		-> .proc_min_ostov_return(*<-return;;*);;
	*);;
*);;

