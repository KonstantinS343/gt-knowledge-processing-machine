scp_program -> proc_add_vertex_edge_attr
(*
	-> rrel_params: ...
	(*
		-> rrel_1: rrel_in: _outline;;	
	*);;

	-> rrel_operators: ... 
	(*
		-> rrel_init: .proc_add_vertex_edge_attr_operator1
		(*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: rrel_common: _vert;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _elements;;
			
			=> nrel_then: .proc_add_vertex_edge_attr_operator2;;
                	=> nrel_else: .proc_add_vertex_edge_attr_operator8;;
		*);;

		-> .proc_add_vertex_edge_attr_operator2
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _elements;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _element;;
			
			=>nrel_then: .proc_add_vertex_edge_attr_operator3;;
			=>nrel_else: .proc_add_vertex_edge_attr_operator7;; 	
		*);;

		-> .proc_add_vertex_edge_attr_operator3
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _element;;
			
			=>nrel_goto: .proc_add_vertex_edge_attr_operator5;; 	
		*);;		

		-> .proc_add_vertex_edge_attr_operator5
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: rrel_oredge;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _arc2;;
			
			=>nrel_goto: .proc_add_vertex_edge_attr_operator6;; 	
		*);;

		-> .proc_add_vertex_edge_attr_operator6
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: .proc_add_vertex_edge_attr_operator2;;
		*);;

		-> .proc_add_vertex_edge_attr_operator7
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _elements;;
			
			=> nrel_goto: .proc_add_vertex_edge_attr_operator8;;
		*);;
		
		-> .proc_add_vertex_edge_attr_operator8
		(*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _vert;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _elements;;
			
			=> nrel_then: .proc_add_vertex_edge_attr_operator9;;
                	=> nrel_else: .proc_add_vertex_edge_attr_return;;
		*);;

		-> .proc_add_vertex_edge_attr_operator9
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _elements;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _element;;
			
			=>nrel_then: .proc_add_vertex_edge_attr_operator10;;
			=>nrel_else: .proc_add_vertex_edge_attr_operator13;; 	
		*);;

		-> .proc_add_vertex_edge_attr_operator10
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _element;;
			
			=>nrel_goto: .proc_add_vertex_edge_attr_operator11;; 	
		*);;		

		-> .proc_add_vertex_edge_attr_operator11
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: rrel_vertex;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arca;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _arc1;;
			
			=>nrel_goto: .proc_add_vertex_edge_attr_operator12;; 	
		*);;

		-> .proc_add_vertex_edge_attr_operator12
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: .proc_add_vertex_edge_attr_operator9;;
		*);;

		-> .proc_add_vertex_edge_attr_operator13
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _elements;;
			
			=> nrel_goto: .proc_add_vertex_edge_attr_return;;
		*);;
		-> .proc_add_vertex_edge_attr_return(*<-return;;*);;
	*);;
*);;

