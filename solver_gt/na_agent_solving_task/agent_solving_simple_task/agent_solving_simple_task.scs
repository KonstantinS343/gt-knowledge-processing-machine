agent_solving_simple_task
=> nrel_main_idtf:
	[агентная scp-программа решения простой задачи] (* <- lang_ru;; *);
	[agent scp-program of solving simple task] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_solving_simple_task (*
	-> rrel_params: .agent_solving_simple_task_params (*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
	*);;

	-> rrel_operators: .agent_solving_simple_task_operator_set (*

		-> rrel_init: .agent_solving_simple_task_operator0 
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;
	
			=> nrel_goto: .agent_solving_simple_task_operator1;;
		*);;

		-> .agent_solving_simple_task_operator1
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_solving_simple_task;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_solving_simple_task_operator2;;
			=> nrel_else: .agent_solving_simple_task_operator_return;;
		*);;

		-> .agent_solving_simple_task_operator2
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _params;;

			=> nrel_then: .agent_solving_simple_task_operator3;;
			=> nrel_else: .agent_solving_simple_task_operator_return;;
		*);;

		-> .agent_solving_simple_task_operator3
		(*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer;;

			=> nrel_goto: .agent_solving_simple_task_operator4;;
		*);;

		-> .agent_solving_simple_task_operator4
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: solution_tree_node;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _ara;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: rrel_const: _solution_tree_root;;

			=> nrel_goto: .agent_solving_simple_task_operator5;;
		*);;

		-> .agent_solving_simple_task_operator5
		(*
			<- call;;
		        -> rrel_1: rrel_fixed: rrel_scp_const: proc_print_idtf;;
		        -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _params;;
				*);;
		        -> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_solving_simple_task_operator6;;
		*);;

		-> .agent_solving_simple_task_operator6
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_solving_simple_task_operator7;;
		*);;		

		-> .agent_solving_simple_task_operator7
		(*
			<- call;;
		        -> rrel_1: rrel_fixed: rrel_scp_const: proc_add_solver_solution_to_outline;;
		        -> rrel_2: rrel_fixed: rrel_scp_const: ... 
				(*
					-> rrel_1: rrel_fixed: rrel_scp_var: _solution_tree_root;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
				*);;
		        -> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_solving_simple_task_operator8;;
		*);;

		-> .agent_solving_simple_task_operator8
		(*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_solving_simple_task_operator_last;;
		*);;	
		
		-> .agent_solving_simple_task_operator_last
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

			=>nrel_goto: .agent_solving_simple_task_operator_return;;
		*);; 

		-> .agent_solving_simple_task_operator_return (*
		  <- return;;
		*);;
	*);;
*);;
