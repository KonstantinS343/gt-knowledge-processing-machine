scp_program -> proc_find_strongly_connected_graph (*	
	-> rrel_params: ... (*
		-> rrel_1: rrel_in: _parameter;;
		-> rrel_2: rrel_in: _answer;;
	*);;

	-> rrel_operators: ... (*

		-> rrel_init: ..proc_add_common_arc1
    (*
        <- call;;
        -> rrel_1: rrel_fixed: rrel_scp_const: proc_add_common_arc;;
        -> rrel_2: rrel_fixed: rrel_scp_const: ... (*
            -> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
        *);;
        -> rrel_3: rrel_assign: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_add_common_arc2;;
    *);;

    -> ..proc_add_common_arc2 
    (*
        <- waitReturn;;
        -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_find_strongly_connected_operator1;;
    *);;

		-> ..proc_find_strongly_connected_operator1 (*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_if_connected;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: ..proc_find_strongly_connected_operator1_1;;
		*);;

		-> ..proc_find_strongly_connected_operator1_1 (*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: ..proc_find_strongly_connected_operator2;;
		*);;

		-> ..proc_find_strongly_connected_operator2 (*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_connected_graph;;

			=> nrel_then: ..proc_find_strongly_connected_operator3_1;;
			=> nrel_else: ..proc_find_strongly_connected_operator_disconnected1;; 
		*);;

	    -> ..proc_find_strongly_connected_operator3_1(*
	        <-eraseEl;;
	        ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_arc2;;
	        =>nrel_goto:..proc_find_strongly_connected_operator3;;
	    *);;


		-> ..proc_find_strongly_connected_operator3 (*
			 <- searchSetStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_scp_var: _el3;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_vertex;;
		
			->rrel_set_3: rrel_assign: rrel_scp_var: _vertexes1;;
			=> nrel_then: ..proc_find_strongly_connected_operator4;;		
		 	=> nrel_else: ..proc_add_graph_in_answer1;;	
		*);;

	    -> ..proc_find_strongly_connected_operator4(*
	        <-searchElStr3;;
	        ->rrel_1:rrel_fixed:rrel_scp_var:_vertexes1;;
	        ->rrel_2:rrel_assign:rrel_scp_var:_arc1;;
	        ->rrel_3:rrel_assign:rrel_scp_var:_elem1;;
	        =>nrel_then:..proc_find_strongly_connected_operator5;;
	        =>nrel_else:..proc_find_strongly_connected_operator_strongly_connected1;;
	    *);;

	    -> ..proc_find_strongly_connected_operator5(*
	        <-eraseEl;;
	        ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_arc1;;
	        =>nrel_goto:..proc_find_strongly_connected_operator6;;
	    *);;

		-> ..proc_find_strongly_connected_operator6 (*
				<- call;;
				-> rrel_1: rrel_fixed: rrel_scp_const: proc_if_or_connected;;
				-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
					-> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
					-> rrel_3: rrel_fixed: rrel_scp_var: _elem1;;
				*);;
				-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

				=> nrel_goto: ..proc_find_strongly_connected_operator7;;
			*);;

		-> ..proc_find_strongly_connected_operator7 (*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: ..proc_find_strongly_connected_operator8;;
		*);;

		-> ..proc_find_strongly_connected_operator8 (*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_connected_graph;;

			=> nrel_then: ..proc_find_strongly_connected_operator9;;
			=> nrel_else: ..proc_find_strongly_connected_operator_weakly_connected0;; 
		*);;

	    -> ..proc_find_strongly_connected_operator9(*
	        <-eraseEl;;
	        ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_arc2;;
	        =>nrel_goto:..proc_find_strongly_connected_operator4;;
	    *);;

	    -> ..proc_find_strongly_connected_operator_weakly_connected0(*
			<- searchElStr3;;
		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_disconnected_graph;;

			=> nrel_then: ..proc_find_strongly_connected_operator_weakly_connected1_1;;
			=> nrel_else: ..proc_find_strongly_connected_operator_weakly_connected1;; 
	    *);;

	    -> ..proc_find_strongly_connected_operator_weakly_connected1_1(*
	        <-eraseEl;;
	        ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_arc2;;
	        =>nrel_goto:..proc_find_strongly_connected_operator_weakly_connected1;;
	    *);;

        -> ..proc_find_strongly_connected_operator_weakly_connected1(*
            <-genElStr5;;
            -> rrel_1: rrel_fixed: rrel_scp_const: concept_weakly_connected_graph;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed:rrel_scp_var: _parameter;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _answer;;

            =>nrel_goto:..proc_find_strongly_connected_operator_weakly_connected2;;
        *);;

        -> ..proc_find_strongly_connected_operator_strongly_connected1(*
            <-genElStr5;;
            -> rrel_1: rrel_fixed: rrel_scp_const: concept_strongly_connected_graph;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed:rrel_scp_var: _parameter;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _answer;;

            =>nrel_goto:..proc_find_strongly_connected_operator_strongly_connected2;;
        *);;

        -> ..proc_find_strongly_connected_operator_disconnected1(*
            <-genElStr5;;
            -> rrel_1: rrel_fixed: rrel_scp_const: concept_disconnected_graph;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed:rrel_scp_var: _parameter;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _answer;;

            =>nrel_goto:..proc_find_strongly_connected_operator_disconnected2;;
        *);;

        -> ..proc_find_strongly_connected_operator_weakly_connected2(*
            <-genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_weakly_connected_graph;;

            =>nrel_goto:..proc_find_strongly_connected_operator10;;
        *);;

        -> ..proc_find_strongly_connected_operator_strongly_connected2(*
            <-genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_strongly_connected_graph;;

            =>nrel_goto:..proc_find_strongly_connected_operator10;;
        *);;

        -> ..proc_find_strongly_connected_operator_disconnected2(*
            <-genElStr3;;
            -> rrel_1:rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_const: concept_disconnected_graph;;

            =>nrel_goto:..proc_find_strongly_connected_operator10;;
        *);;

        -> ..proc_find_strongly_connected_operator10(*
            <-genElStr3;;
            -> rrel_1:rrel_fixed:rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _parameter;;

            =>nrel_goto:..proc_find_strongly_connected_operator11;;
        *);;

	    -> ..proc_find_strongly_connected_operator11(*
	        <-eraseEl;;
	        ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var:_vertexes1;;
	        =>nrel_goto: ..proc_add_graph_in_answer1;;
	    *);;

	    -> ..proc_add_graph_in_answer1 
    (*
        <- call;;
        -> rrel_1: rrel_fixed: rrel_scp_const: proc_generate_full_answer;;
        -> rrel_2: rrel_fixed: rrel_scp_const: ... (*
            -> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
        *);;
        -> rrel_3: rrel_assign: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_add_graph_in_answer2;;
    *);;

        -> ..proc_add_graph_in_answer2 
    (*
        <- waitReturn;;
        -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_remove_common_arc1;;
    *);;

    -> ..proc_remove_common_arc1
    (*
        <- call;;
        -> rrel_1: rrel_fixed: rrel_scp_const: proc_remove_common_arcs;;
        -> rrel_2: rrel_fixed: rrel_scp_const: ... (*
            -> rrel_1: rrel_fixed: rrel_scp_var: _parameter;;
        *);;
        -> rrel_3: rrel_assign: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_remove_common_arc2;;
    *);;

    -> ..proc_remove_common_arc2 
    (*
        <- waitReturn;;
        -> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

        => nrel_goto: ..proc_find_strongly_connected_operator_return;;
    *);;

		-> ..proc_find_strongly_connected_operator_return (*
			<- return;;
		*);;

	*);;
*);;
